숙제) 

오늘날짜의 가장 많이 판매된 세권의 도서의 도서별 총 판매수를 출력하는 view를 만들고 결과를 출력하는 웹문서를 작성

-기존에 있는 데이터 수정 (판매번호 15번 이후의 판매일은 오늘날짜로 수정)

`update orders set orderdate = sysdate where orderid >= 15;`

-오늘 날짜에 판매된 도서이름별 판매수
```
select bookname, count(saleprice) cnt 
from book b, orders o 
where b.bookid = o.bookid and
to_char(sysdate,'yyyy/mm/dd') = to_char(orderdate,'yyyy/mm/dd')
group by bookname
order by count(saleprice) desc;
```

-인라인뷰를 이용
```
select * from (select bookname, count(saleprice) cnt 
from book b, orders o 
where b.bookid = o.bookid and
to_char(sysdate,'yyyy/mm/dd') = to_char(orderdate,'yyyy/mm/dd')
group by bookname
order by count(saleprice) desc)
where rownum <=3;
```

-뷰 생성
```
create view today_top3 as
select * from (select bookname, count(saleprice) cnt 
from book b, orders o 
where b.bookid = o.bookid and
to_char(sysdate,'yyyy/mm/dd') = to_char(orderdate,'yyyy/mm/dd')
group by bookname
order by count(saleprice) desc)
where rownum <=3;
```

`select * from today_top3;`

-프로그램만들기

bookname과 cnt포장


# PL/SQL : 오라클이 제공하는 프로그래밍 언어
## PL/SQL 종류
### procedure
자바의 메소드처럼 만들어 두고 호출하여 insert, update, delete, select를 할 수 있음

매개변수가 있을 수도 있고, 매개변수가 없을 수도 있음

return값이 있을 수도 있고, return값이 없을 수도 있음 

### trigger
sql명령어의 이벤트처리를 위한 것으로써

어떤 A테이블에 insert, update, delete가 발생되면 

연관있는 B테이블에 연쇄하여 insert, update, delete를 자동으로 하게 만드는 것

### function
프로시저와 마찬가지로 사용 되는데 이것은 select절에 사용할 용도로 만듬

반드시 반환값이 있도록 만들어야 함

`select count(saleprice) from orders;`

위의 sql에서 사용한 count처럼 select절에 사용할 함수를 만들때에는 function으로 만듬

## 프로시저 만들기
```
create or replace procedure 프로시저이름
(
	매개변수1 [모드] 자료형,
	매개변수2 [모드] 자료형,
	...
)
as
	지역변수명1 자료형;
	지역변수명2 자료형;
begin
	프로시저가 해야할 명령어(들);
end;
/
```

**모드에는 in, out이 있음**

in은 프로시저를 실행하기에 필요한 값을 읽어 들이기 위한 입력용

out은 프로시저 수행한 결과를 반환하기 위한 출력용

out모드의 변수를 여러개 전달받아 여러개의 값을 반환할 수 있음

또, 모드를 생략하면 기본은 in

**자료형은 어떤 테이블의 컬럼의 자료형과 동일한 자료형을 표현할 때에는 테이블.컬럼명%TYPE으로 표현**

**begin과 end; 사이 프로시저에 해야할 명령어들을 써줌**

여기에는 sql명령어(insert, update, delete, select)가 올 수 있으며 

또, 반복문과 제어문 등을 표현할 수 있음

pl/sql에서 대입연산자는 := 이렇게 표현함

연습) 새로운 부서를 등록하는 프로시저를 생성
```
create or replace procedure insert_dept(
	p_dno in dept.dno%TYPE,
	p_dname in dept.dname%TYPE,
	p_dloc in dept.dloc%TYPE
)
as
begin
	insert into dept(dno, dname, dloc) values(p_dno, p_dname, p_dloc);
end;
/
```
`exec insert_dept(50,'개발3팀','제주');`


연습) 부서번호와 부서위치를 매개변수로 전달받아 해당부서의 정보를 수정하는 프로시저를 만들어봄
```
create or replace procedure update_dloc(
	p_dno in dept.dno%type,
	p_dloc in dept.dloc%type
)
as
begin
	update dept set dloc = p_dloc where dno = p_dno;
end;
/
```

`exec update_dloc(50,'서교동');`

연습) 부서번호를 매개변수로 전달받아 해당부서를 삭제하는 프로시저를 생성하고 호출
```
create or replace procedure delete_dept(
	p_dno in dept.dno%type
)
as
begin
	delete dept where dno = p_dno;
end;
/
```

연습) 부서번호를 전달받아 해당 부서의 위치를 출력하는 프로시저
```
set serveroutput on;
create or replace procedure print_dloc(
	p_dno dept.dno%type
)
as
	p_dloc dept.dloc%type;
begin
	select dloc into p_dloc from dept where dno = p_dno;
	dbms_output.put_line(p_dloc);
end;
/
```
`exec print_dloc(10);`

연습) 부서번호를 전달받아 해당 부서의 위치를 반환하는 프로시저
```
create or replace procedure get_dloc(
	p_dno in dept.dno%type,
	p_dloc out dept.dloc%type
)
as
begin
	select dloc into p_dloc from dept where dno = p_dno;
end;
/
```
```
declare
	result dept.dloc%type;
begin
	get_dloc(10,result);
	dbms_output.put_line(result);
end;
/
```

## cursor의 사용
프로시저 안에서 select한 행의 수가 여러건 일때는 cursor를 사용해야함

연습) 부서위치를 매개변수로 전달받아 그 위치에 있는 모든 부서명을 출력하는 프로시저를 생성하고 호출
```
create procedure print_dname(
	p_dloc in dept.dloc%type
)
as
	p_dname dept.dname%type;
	cursor c is select dname from dept where dloc = p_dloc;
begin
	open c;
	loop
		fetch c into p_dname;
		exit when c%NOTFOUND;
		dbms_output.put_line(p_dname);
	end loop;
	close c;
end;
/
```
`exec print_dname('서교동');`

트리거 : SQL에서의 이벤트 처리를 위한 것으로써 어떠한 테이블에 이벤트(insert, update, delete)가 일어날 때에 자동으로 동작하는 프로시저를 말함 

실습을 위하여 dept 테이블을 복사하여 dept_back테이블을 생성 `create table dept_back as select * from dept;`

## 트리거를 만드는 방법
```
create or replace trigger 트리거이름
시점 명령어 on 테이블이름 for each row
declare
	변수명1 자료형
	변수명2 자료형
begin
	트리거가 해야할 명령어(들)
end;
/
```

**시점에는 before , after가 올 수 있음**

**명령어에는 insert, update, delete가 올 수 있음**
```
:new = insert나 update가 된 새로운 행을 의미하는 키워드
:old = delete나 update 되기 전의 행을 의미하는 키워드
```
연습) dept테이블에 insert가 된 후에 똑같은 레코드를 dept_back 테이블에 자동으로 insert하는 트리거를 만들어 보고 dept에 insert 해봄
```
create or replace trigger trg_afterInsertDept
after insert on dept for each row
declare 
begin
	insert into dept_back values(:new.dno, :new.dname, :new.dloc);
end;
/
```

트리거가 잘 동작하는지 확인하기 위하여 dept테이블에 레코드를 추가해봄 dept_back에도 자동으로 추가되는지 확인

`insert into dept values(50, '개발3팀', '제주');`

연습) dept테이블에 수정이 되면 자동으로 dept_back의 내용도 수정하도록 트리거를 생성하고 dept 테이블의 내용을 수정해봄
```
create or replace trigger trg_afterUpdateDept
after update on dept for each row
declare
begin
	update dept_back set dname = :new.dname , dloc = :new.dloc where dno = :new.dno;
end;
/
```

연습) dept테이블에 삭제가 일어나면 자동으로 dept_back에도 삭제가 되도록 트리거를 생성하고 dept테이블의 행을 삭제해봄
```
create or replace trigger trg_afterdeleteDept
after delete on dept for each row
declare
begin
	delete dept_back where dno= :old.dno;
end;
/
```

실습을 위해 테이블 추가
```
create table 상품(
	상품번호 number primary key,
	상품명	varchar2(30),
	가격 number,
	재고수량 number
);
```
```
create table 입고(
	입고번호 number primary key,
	상품번호 number references 상품(상품번호),
	입고수량 number,
	입고일 date
);
```
```
insert into 상품 values(100,'색종이',500,0);
insert into 상품 values(200,'삼각자',700,0);
insert into 상품 values(300,'딱풀',600,0);
```

연습) 입고테이블에 insert가 일어나면 상품테이블에 입고한 수량만큼 재고수량을 증가하는 트리거를 만들고 입고테이블에 insert를 수행
```
create or replace trigger trg_insert입고
after insert on 입고 for each row
declare
begin
	update 상품 set 재고수량 = 재고수량+ :new.입고수량 where 상품번호 = :new.상품번호;
end;
/
```

연습) 입고에서 삭제가 일어나면 삭제한 입고수량만큼 상품의 재고수량을 감소하는 트리거를 만들고 입고에서 삭제를 수행하여 결과를 확인
```
create or replace trigger trg_delete입고
after delete on 입고 for each row
declare
begin
	update 상품 set 재고수량 = 재고수량  - :old.입고수량 where 상품번호 = :old.상품번호;
end;
/
```
```
 insert into 입고 values(1,100,10,sysdate);
 insert into 입고 values(2,200,10,sysdate);
 insert into 입고 values(3,300,50,sysdate);
```

연습) 입고테이블에 수정이 될때에 자동으로 동작하여 상품테이블에 재고수량을 수정하는 트리거를 만들고 입고테이블에서 입고수량을 수정 해 봄
```
create or replace trigger trg_update입고
after update on 입고 for each row
declare
begin
	update 상품 set 재고수량 = 재고수량 - (:old.입고수량 - :new.입고수량) 
	where 상품번호 = :new.상품번호;
end;
/
```
```
SQL> select * from 입고;

입고번호 상품번호 입고수량 입고일
-------- -------- -------- ---------------
       2      200       10 21/11/23
       3      300       50 21/11/23
       1      100       10 21/11/23
```
```
SQL> select * from 상품;

상품번호 상품명         가격 재고수량
-------- ---------- -------- --------
     100 색종이          500       10
     200 삼각자          700       10
     300 딱풀            600       50
```
```
SQL> insert into 입고 values(4,300,100,sysdate);

1 개의 행이 만들어졌습니다.
```
```
SQL> select * from 입고;

입고번호 상품번호 입고수량 입고일
-------- -------- -------- ---------------
       4      300      100 21/11/23
       2      200       10 21/11/23
       3      300       50 21/11/23
       1      100       10 21/11/23
```
```
SQL> select * from 상품;

상품번호 상품명         가격 재고수량
-------- ---------- -------- --------
     100 색종이          500       10
     200 삼각자          700       10
     300 딱풀            600      150
```
```
SQL> update 입고 set 입고수량 = 30 where 입고번호 = 3;

1 행이 업데이트되었습니다.
```
```
SQL> select * from 상품;

상품번호 상품명         가격 재고수량
-------- ---------- -------- --------
     100 색종이          500       10
     200 삼각자          700       10
     300 딱풀            600      130
```
```
SQL> select * from 상품;

상품번호 상품명         가격 재고수량
-------- ---------- -------- --------
     100 색종이          500       10
     200 삼각자          700       10
     300 딱풀            600      130
```
```
SQL> update 입고 set 입고수량 = 150 where 입고번호 = 4;

1 행이 업데이트되었습니다.
```
```
SQL> select * from 상품;

상품번호 상품명         가격 재고수량
-------- ---------- -------- --------
     100 색종이          500       10
     200 삼각자          700       10
     300 딱풀            600      180
```
```
SQL> select * from 입고;

입고번호 상품번호 입고수량 입고일
-------- -------- -------- ---------------
       4      300      150 21/11/23
       2      200       10 21/11/23
       3      300       30 21/11/23
       1      100       10 21/11/23
```
# PL/SQL 
## procedure
빈번하게 사용하는 insert, update, delete, select를 정의 해 두고 쉽게 사용 가능

## trigger
sql에서의 이벤트 처리 위한 프로시저의 일종

이것을 사용자가 호출하는 것이 아니라 이벤트가 발생되면 자동으로 동작하는 프로시저임

## function
select절에 사용할 수 있는 함수

오라클이 제공하는 함수들이 많이 있음 sum, max, min, count, trim, ...

이러한 함수들은 모두 select절에 사용할 수 있음

이것들 처럼 사용자가 select절에 사용할 수 있는 사용자가 필요한 함수를 만들 수 있음

### 사용자 정의 함수(function)
**function 만드는 방법**
```
create or replace function 함수명(매개변수명 자료형) return 자료형
is
	지역변수(들)
begin
	함수가 해야할 문장(들)
	return 값;
end;
/
```

연습) 판매금액을 매개변수로 전달받아 이익금을 반환하는 사용자 정의 함수를 만들어 봄

사용자 정의 함수를 만들어서 select에서 사용해봄

판매금액이 30000원 이상이면 10% 그렇지 않으면 5%가 이익금
```
create or replace function profit (saleprice number) return number
is
	result number;
begin
	if saleprice >=30000 then
	result := saleprice*0.1;
	else
	result := saleprice*0.05;
	end if;
	return result;
end;
/
```

연습) 오늘 판매된 주문에 대하여 도서명, 도서가격, 판매가격, 이익금을 출력
```
select bookname, price, saleprice, profit(saleprice) 이익금
from book b, orders o
where b.bookid = o.bookid and
to_char(sysdate,'yyyy/mm/dd') = to_char(orderdate,'yyyy/mm/dd');
```

연습) 고객번호를 매개변수로 전달받아 그 고객의 주문총액을 계산하여 

주문총액이 20000원 이상이면 '우수' 그렇지 않으면 '보통'을 반환하는 Grade()함수를 만들고 호출
```
create or replace function Grade(f_custid number) return varchar2
is
	result varchar2(30);
	tot number;
begin
	select sum(saleprice) into tot from orders where custid = f_custid;
	if tot >= 20000 then 
	result := '우수';
	else
	result := '보통';
	end if;
	return result;
end;
/
```

고객의 이름과 고객의 등급 출력

`select name, Grade(custid) grade from customer;`
```
NAME       GRADE
---------- ----------
박세리     보통
안철수     보통
심상정     보통
윤석열     우수
이재명     우수
Tiger      보통
tiger      보통
TIGER      보통
   tiger   보통
TIGER      보통
Tiger      보통
박지성     우수
김연아     우수
장미란     우수
추신수     우수
```

고객의 이름과 고객의 총 주문금액, 고객의 등급을 출력

`select name, (select sum(saleprice)  from orders o where o.custid = c.custid ) sum, grade(custid) grade from customer c;`
```
NAME              SUM GRADE
---------- ---------- ----------
장미란          71000 우수
김연아          35000 우수
윤석열          88500 우수
이재명          55000 우수
박지성         108000 우수
추신수          33000 우수
안철수                보통
심상정                보통
   tiger              보통
Tiger                 보통
TIGER                 보통
박세리                보통
TIGER                 보통
tiger                 보통
Tiger                 보통
```

연습) 주소를 매개변수로 전달받아 국내에 거주하면 '국내거주' 해외에 거주하면 '국외거주'를 반환하는 함수를 만들고 호출 select문에서 호출해봄 

`select * from customer where address like`
```
create or replace function Domestic(f_address varchar2) return varchar2
is
		result varchar2(30);
begin
	if f_address like '대한민국%' then
	result := '국내거주';
	else
	result := '국외거주';
	end if;
	return result;
end;
/
```

고객의 이름과 국내거주 여부를 출력

`select name, Domestic(address) domestic from customer;`
```
NAME       DOMESTIC
---------- ----------
박세리     국내거주
안철수     국내거주
심상정     국내거주
윤석열     국내거주
이재명     국내거주
Tiger      국내거주
tiger      국내거주
TIGER      국내거주
   tiger   국내거주
TIGER      국내거주
Tiger      국내거주
박지성     국외거주
김연아     국내거주
장미란     국내거주
추신수     국외거주
```

연습) 두개의 함수를 호출하여 고객명, 고객의 등급, 국내거주여부를 출력 

`select name, Grade(custid) grade, Domestic(address) domestic from customer;`
```
NAME       GRADE      DOMESTIC
---------- ---------- ----------
박세리     보통       국내거주
안철수     보통       국내거주
심상정     보통       국내거주
윤석열     우수       국내거주
이재명     우수       국내거주
Tiger      보통       국내거주
tiger      보통       국내거주
TIGER      보통       국내거주
   tiger   보통       국내거주
TIGER      보통       국내거주
Tiger      보통       국내거주
박지성     우수       국외거주
김연아     우수       국내거주
장미란     우수       국내거주
추신수     우수       국외거주
```
```
select name, Grade(custid) grade, Domestic(address) domestic 
from customer
order by Domestic(address);
```
```
NAME       GRADE      DOMESTIC
---------- ---------- ----------
박세리     보통       국내거주
안철수     보통       국내거주
장미란     우수       국내거주
심상정     보통       국내거주
윤석열     우수       국내거주
이재명     우수       국내거주
Tiger      보통       국내거주
tiger      보통       국내거주
TIGER      보통       국내거주
   tiger   보통       국내거주
TIGER      보통       국내거주
Tiger      보통       국내거주
김연아     우수       국내거주
박지성     우수       국외거주
추신수     우수       국외거주
```

연습) Domestic 함수를 이용하여 국내외 거주지별 총판매액을 출력
```
select Domestic(address) domestic , sum(saleprice) sum 
from orders o , customer c
where o.custid = c.custid 
group by Domestic(address);
```
```
DOMESTIC          SUM
---------- ----------
국내거주       249500
국외거주       141000
```

팀별 연습문제 
모든 도서목록을 출력하는 jsp를 작성
(도서목록에는 도서번호와 도서명만 출력)
또, 도서목록에서 도서명을 누르면 도서 상세보기로 이동하는 jsp도 작성 
















